***************************************************
* Live Acquisition Script (2.0)                   *
* - Pulls Forensics artifacts for analysis        *
* - Created by D0n Quixote/OMENScan (May 2015)    *
* - Inspired by many other tools                  *
*                                                 *
* - IMPORTANT NOTE: IT IS BEST TO MOUNT THE E01   *
*   AS TEMPORARY WRITE - MANY UTILS FAIL on R/O   *
*                                                 *
* - It also helps to browse to the system         *
*   directories on the mounted drive to be sure   *
*   you have permissions to read the files.       *
*                                                 *
* Achoir /ini:DeadBox.acq /Drv:<x:>               *
***************************************************
SAY:
SAY: AChoir will Pull Artifacts from the Drive Mounted as &DRv
SAY:  - To properly do this use: Achoir /ini:DeadBox.acq /Drv:<x:>
SAY:
SAY: * * *   Please MAKE SURE you are running this as an Administrator  * * * 
SAY: * * * Administrator right are required for many of these utilities * * * 
SAY:
SAY:Checking for the presence of Windows on the Mounted Drive:
SAY:   &Drv\Windows\System32\ntoskrnl.exe
SAY:
REQ:&Drv\Windows\System32\ntoskrnl.exe
SAY:
PZZ: Press Any Key to Continue, or q to QUIT
SAY:
SAY: 1a. Copy Raw $MFT...
SAY:
ACQ:\RawData
NCP:"&Drv\$MFT" "&Acq"
SAY:
SAY: 1b. Now Parsing $MFT...
SAY:
EXE:\DSK\MFTDump.exe /l /d /v --output=&Acq\MFTDump.dat &Acq\$MFT 
SAY:
SAY: 1c. Copy Raw $LogFile...
SAY:
NCP:"&Drv\$LogFile" "&Acq"
SAY:
SAY: 1d. Now Parsing USNJrnl...
SAY:
CMD:fsutil usn enumdata 1 0 1 &Drv > &Acq\USNJrnl.dat
CMD:fsutil usn readjournal &Drv csv >> &Acq\USNJrnl.dat
SAY:
SAY: 1e. Now Extracting USNJrnl...
SAY:
EXE:\DSK\ExtractUSNJrnl64.exe /DevicePath:&Drv /OutputPath:&Acq
SAY:
SAY: 2a. Parsing and Copying System Registry Hives/Files...
SAY:
ACQ:\Reg
RegFileExport "&Drv\Windows\system32\config\SAM" "Reg-SAM.txt"
RegFileExport "&Drv\Windows\system32\config\SECURITY" "Reg-SECURITY.txt"
RegFileExport "&Drv\Windows\system32\config\SOFTWARE" "Reg-SOFTWARE.txt"
RegFileExport "&Drv\Windows\system32\config\SYSTEM" "Reg-SYSTEM.txt"
For:&Drv\Windows\system32\config\*.*
CPY:"&For" "&Acq\&Fnm"
CPY:"&Drv\Windows\AppCompat\Programs\Amcache.hve" "&Acq\Amcache.hve"
SAY:
SAY: 2a. Parsing and Copying USER Registry Hives/Files...
SAY:
FOR:&Drv\Users\NTUSER.DAT
EXE:\SYS\RegfileExport.exe &For "&Acq\NTUSER(&Num).EXP"
CPY:"&For" "&Acq\&Fnm(&Num)"
SAY:
SAY: 2b. RegRipping USER Registry Hives/Files...
DIR:\RRV\RegRipper3.0-master
CMD:rip.exe -r &FOR -f ntuser > &Acq\NTUSER(&NUM).regrip
SAY:
SAY: 2c. RegRipping Common Hives...
SAY:
SAY: ***Scanning Software hive***
CMD:rip.exe -r &Drv\Windows\System32\Config\software -f software > &Acq\Software.regrip
SAY:
SAY: ***Scanning System hive***
CMD:rip.exe -r &Drv\Windows\System32\Config\system -f system > &Acq\System.regrip 
SAY:
SAY: ***Scanning SAM hive***
CMD:rip.exe -r &Drv\Windows\System32\Config\sam -f sam > &Acq\Sam.regrip
SAY:
SAY: ***Scanning Security hive*** 
CMD:rip.exe -r &Drv\Windows\System32\Config\SECURITY –f security > &Acq\Security.regrip
SAY:
SAY: 3. Parsing Prefetch Directory...
DIR:\
SAY:
ACQ:\Prf
For:&Drv\Windows\prefetch\*.*
CPY:"&For" "&Acq\&Fnm"
CMD:Dir &Drv\Windows\prefetch /a /q /r > &Acq\Prefetch.dat
EXE:\SYS\WinPrefetchView.exe /folder &Drv\Windows\Prefetch /scomma &Acq\WinPrefetchview.dat
SAY:
SAY: 4. Parsing Etc Directory, and SRUM...
SAY:
ACQ:\SYS
ACQ:\SYS\Sys32
For:&Drv\Windows\System32\Drivers\Etc\*.*
CPY:"&For" "&Acq\&Fnm"
CPY:"&Drv\Windows\System32\sru\SRUDB.dat" "&Acq\SRUDB.dat"
ACQ:\SYS\Nativ
CPY:"&Drv\Windows\SysWOW64\sru\SRUDB.dat" "&Acq\SRUDB.dat"
SAY:
SAY: 5. Gathering AutoRun Information...
SAY:
ACQ:\Arn
SYS:&Dir\Sys\Autorunsc.exe -c -z &Drv\Windows &Drv\Users\default >> &Acq\Autorun.dat
SYS:for /d %%z in (&Drv\Users\*) do &Dir\Sys\Autorunsc.exe -c -z &Drv\Windows %%z >> &Acq\AutorunAll.dat
***:EXE:\SYS\Autoruns.exe
SAY:
SAY: 6a. Parsing EventViewer Logs Hives...
SAY:
ACQ:\Evt
CMD:\SYS\PSLogList.exe /accepteula -s -l &Drv\Windows\System32\Winevt\logs\Security.evtx  > &Acq\EvtSec.dat
CMD:\SYS\PSLogList.exe /accepteula -s -l &Drv\Windows\System32\Winevt\logs\Application.evtx > &Acq\EvtApp.dat
CMD:\SYS\PSLogList.exe /accepteula -s -l &Drv\Windows\System32\Winevt\logs\System.evtx > &Acq\EvtSys.dat
SAY:
SAY: 6b. Copying (System32) EventViewer Logs Hives...
SAY:
ACQ:\Evt\Sys32
For:&Drv\Windows\System32\winevt\Logs\*.*
CPY:"&For" "&Acq\&Fnm"
SAY:
SAY: 6c. Copying (Sysnative) EventViewer Logs...
SAY:
ACQ:\Evt\Nativ
For:&Drv\Windows\SysWOW64\winevt\Logs\*.*
CPY:"&For" "&Acq\&Fnm"
SAY:
SAY: 7. Now Extracting Browser History...
SAY:
ACQ:\Brw
EXE:\SYS\BrowsingHistoryView.exe /HistorySource 3 /HistorySourceFolder "&Drv\Users" /VisitTimeFilterType 1 /scomma "&Acq\BrowseHist.csv" 
EXE:\SYS\BrowsingHistoryView.exe /HistorySource 3 /HistorySourceFolder "&Drv\Users" /VisitTimeFilterType 1 /shtml "&Acq\BrowseHist.htm" 
SAY:
SAY: 8. Parsing Offline Registry AutoRun Keys
SAY:
ACQ:\Arn
ARN:&Drv\Windows\System32\Config\SOFTWARE
SAY:
SAY: 9. Gathering $Recycle.Bin entries...
SAY:
ACQ:\RBin
NCP:"&Drv:\$Recycle.Bin\*" "&Acq"
SAY:
SAY: Acquisition Complete...
SAY:
SAY: Now Hashing Artifacts...
HSH:ACQ
SAY:
PZZ: All Done!  Press Any Key to Finish.
